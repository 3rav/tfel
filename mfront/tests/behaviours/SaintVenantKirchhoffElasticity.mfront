@Parser DefaultFiniteStrainParser;
@Behaviour SaintVenantKirchhoffElasticity;
@Author T. Helfer;
@Date 19/10/2013;

@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

@LocalVariable stress lambda;
@LocalVariable stress mu;

@Includes{
#include"TFEL/Material/Lame.hxx"
}

@ProvidesSymmetricTangentOperator;
@Integrator{
  using namespace tfel::material::lame;
  // Lame coefficients
  lambda = computeLambda(young,nu);
  mu     = computeMu(young,nu);
  // Green-Lagrange tensor
  const StrainStensor e = computeGreenLagrangeTensor(F1);
  // second Piolaâ€“Kirchhoff stress
  const StressStensor s = lambda*trace(e)*StrainStensor::Id()+2*mu*e;
  // convertion to Cauchy stress tensor
  sig = convertSecondPiolaKirchhoffStressToCauchyStress(s,F1);
  if(computeTangentOperator_){
    StiffnessTensor De = 0.5*(lambda*Stensor4::IxI()+2*mu*Stensor4::Id());
    if(smflag==DS_DEGL){
      Dt = De;
    } else if(smflag==DS_DC){
      Dt = 0.5*De;
    } else if(smflag==DS_DF){
      Dt = De*t2tost2<N,strain>::dCdF(F1);
    } else if (smflag==DS_DDF){
      Dt = De*t2tost2<N,strain>::dCdF(F1)*t2tot2<N,real>::tpld(F0);
    } else if (smflag==DTAU_DF){
      t2tost2<N,stress> dS = De*t2tost2<N,strain>::dCdF(F1);
      t2tost2<N,stress> dtau;
      computePushForwardDerivative(dtau,dS,s,F1); 
      Dt = dtau;
    } else if (smflag==DTAU_DDF){
      t2tost2<N,stress> dS = De*t2tost2<N,strain>::dCdF(F1);
      t2tost2<N,stress> dtau;
      computePushForwardDerivative(dtau,dS,s,F1);
      Dt = dtau*t2tot2<N,real>::tpld(F0); 
    } else if (smflag==DTAU_DF){
      t2tost2<N,stress> dS = De*t2tost2<N,strain>::dCdF(F1);
      t2tost2<N,stress> dtau;
      t2tost2<N,stress> dsig;
      computePushForwardDerivative(dtau,dS,s,F1); 
      computeCauchyStressDerivativeFromKirchoffDerivative(dsig,dtau,sig,F1); 
      Dt = dsig;
    } else if (smflag==DTAU_DDF){
      t2tost2<N,stress> dS = De*t2tost2<N,strain>::dCdF(F1);
      t2tost2<N,stress> dtau;
      t2tost2<N,stress> dsig;
      computePushForwardDerivative(dtau,dS,s,F1); 
      computeCauchyStressDerivativeFromKirchoffDerivative(dsig,dtau,sig,F1); 
      Dt = dsig*t2tot2<N,real>::tpld(F0); 
    } else {
      string msg("SaintVenantKirchhoffElasticity : "
		 "unsupported tangent operator");
      throw(runtime_error(msg));
    }
  }
}
