@Parser    RungeKutta;
@Behaviour NortonRK2;
@Author    Helfer Thomas;
@Algorithm rk54;
@Date 23/11/06;

@Epsilon 1.e-8;

@Includes{
#include"TFEL/Material/Lame.hxx"
}

@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

@MaterialProperty real A;     /* Norton coefficient   */
@MaterialProperty real E;     /* Norton exponant      */

@LocalVariable real lambda; /* first  Lame coefficient  */
@LocalVariable real mu;     /* second Lame coefficient */

@StateVariable real    p[100];   /* Equivalent viscoplastic strain */
@StateVariable Stensor evp[100]; /* Viscoplastic strain            */

/* Initialize Lame coefficients */
@InitLocalVariables{
  using namespace tfel::material::lame;
  lambda = computeLambda(young,nu);
  mu = computeMu(young,nu);
}

@PredictionOperator{
  if(!((smt==ELASTIC)||(smt==SECANTOPERATOR)||(smt==TANGENTOPERATOR))){
    return FAILURE;
  }
  Dt=lambda*Stensor4::IxI()+2*mu*Stensor4::Id();
}

@IsTangentOperatorSymmetric true;
@TangentOperator{
  if(!((smt==ELASTIC)||(smt==SECANTOPERATOR)||(smt==TANGENTOPERATOR))){
    return FAILURE;
  }
  Dt=lambda*Stensor4::IxI()+2*mu*Stensor4::Id();
}

@ComputeStress{
  sig = lambda*trace(eel)*StrainStensor::Id()+2*mu*eel;
}

@Derivative{
  real sigeq = sigmaeq(sig);
  if(sigeq>1.e9){
    return false;
  }
  Stensor  n(0.);
  if(sigeq > 10.e-7){
    n    = 1.5f*deviator(sig)/sigeq;
  }
  deel = deto;
  const real vp = (A/evp.size())*pow(sigeq,E); 
  for(unsigned short i=0;i!=evp.size();++i){
    dp[i]    = vp;
    devp[i]  = dp[i]*n;
    deel    -= devp[i];
  }
}
