/*! 
 * \file    castemsaintvenantkirchhoffelasticity-shear-4.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  26 nov. 2013
 */

@Behaviour<umat> '@TFEL_LOCAL_LIBRARY_PREFIX@libMFrontCastemBehaviours@CMAKE_SHARED_LIBRARY_SUFFIX@' 'umatsaintvenantkirchhoffelasticity';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;
@MaterialProperty<constant> 'MassDensity'        0.;
@MaterialProperty<constant> 'ThermalExpansion'   0.;

@ImposedDrivingVariable 'FXX' 1.;
@ImposedDrivingVariable 'FYY' 1.;
@ImposedDrivingVariable 'FZZ' 1.;
@ImposedDrivingVariable 'FXY' 0.;
@ImposedDrivingVariable 'FYX' 0.;
@ImposedDrivingVariable 'FXZ' 0.;
@ImposedDrivingVariable 'FZX' {0:0.,2000.:0.2};
@ImposedDrivingVariable 'FYZ' 0.;
@ImposedDrivingVariable 'FZY' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,2000 in 100};

@Real 'lambda' 'PoissonRatio*YoungModulus/((1+PoissonRatio)*(1-2*PoissonRatio))';
@Real 'mu'     'YoungModulus/(2*(1+PoissonRatio))';
@Test<function> {'SXX':'((2*mu+lambda)*FZX**2)/2'} 1.e-2;
@Test<function> {'SYY':'(lambda*FZX**2)/2 '} 1.e-2;
@Test<function> {'SZZ':'((2*mu+lambda)*FZX**4+(4*mu+lambda)*FZX**2)/2'} 1.e-2;
@Test<function> {'SXZ':'sqrt(2)*((2*mu+lambda)*FZX**3+2*mu*FZX)/2'} 1.e-2;
@Test<function> {'SXY':'0.','SYZ':'0.'} 1.e-2;

